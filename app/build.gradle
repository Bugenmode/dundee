apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'


android {
	compileSdkVersion 27
	defaultConfig {
		applicationId "com.strv.dundee"
		minSdkVersion 21
		targetSdkVersion 27
		versionCode gitTagCount
		versionName gitTagName
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	signingConfigs {
		release {
			storeFile file(signingKeyStoreFile)
			keyAlias signingKeyAlias
			keyPassword signingKeyPassword
			storePassword signingKeyPassword
		}
	}

	buildTypes {
		release {
			postprocessing {
				removeUnusedCode false
				removeUnusedResources false
				obfuscate false
				optimizeCode false
				proguardFile 'proguard-rules.pro'
				signingConfig signingConfigs.release
			}
		}
	}

	testOptions {
		unitTests {
			includeAndroidResources = true
		}
	}

	androidExtensions {
		experimental = true
	}

	flavorDimensions "default"

	productFlavors {

		finalProduction {
			dimension "default"
			applicationId "com.strv.dundee"
			resValue "string", "app_name", "Project Dundee"
			buildConfigField "boolean", "LOGS", "false"
		}

		demoProduction {
			dimension "default"
			applicationId "com.strv.dundee.demo"
			resValue "string", "app_name", "Project Dundee Demo"
			buildConfigField "boolean", "LOGS", "false"
		}

		develop {
			dimension "default"
			applicationId "com.strv.dundee.develop"
			resValue "string", "app_name", "Project Dundee Develop"
			buildConfigField "boolean", "LOGS", "true"
		}

		mock {
			dimension "default"
			applicationId "com.strv.dundee.mock"
			resValue "string", "app_name", "Project Dundee Mock"
			buildConfigField "boolean", "LOGS", "true"
		}
	}


	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
		androidTest.java.srcDirs += 'src/androidTest/kotlin'
	}
	dataBinding.enabled true

	applicationVariants.all {variant ->
		if (variant.buildType.name == "release") {
			variant.outputs.all {output ->
				def date = new Date().format('yyyyMMdd')
				outputFileName = "${appName}-${variant.name}-${variant.versionName}-${variant.versionCode}-${date}.apk"
			}
			aaptOptions.cruncherEnabled true
		} else {
			aaptOptions.cruncherEnabled false
		}
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

	// Android Support Library
	implementation "com.android.support:appcompat-v7:$supportLibVersion"
	implementation "com.android.support:cardview-v7:$supportLibVersion"
	implementation "com.android.support:design:$supportLibVersion"
	implementation "com.android.support:customtabs:$supportLibVersion"

	implementation "com.android.support.constraint:constraint-layout:1.0.2"

	// Firebase
	implementation "com.google.firebase:firebase-core:$firebaseVersion"
	implementation "com.google.firebase:firebase-firestore:$firebaseVersion"
	implementation "com.google.firebase:firebase-auth:$firebaseVersion"
	implementation "com.google.android.gms:play-services-auth:$firebaseVersion"
	implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
		transitive = true
	}

	// Android Arch
	implementation "android.arch.lifecycle:extensions:$archVersion"
	implementation "android.arch.persistence.room:runtime:$archVersion"
	kapt "android.arch.persistence.room:compiler:$archVersion"

	// Tatarka
	implementation 'me.tatarka.bindingcollectionadapter2:bindingcollectionadapter:2.3.0-beta1'
	implementation 'me.tatarka.bindingcollectionadapter2:bindingcollectionadapter-recyclerview:2.3.0-beta1'

	// Retrofit
	implementation "com.squareup.retrofit2:retrofit:2.3.0"
	implementation "com.squareup.retrofit2:converter-gson:2.3.0"
	implementation "com.squareup.okhttp3:logging-interceptor:3.9.0"

	// Charts
	implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'

	// Tests
	testImplementation 'junit:junit:4.12'
	testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
	testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
	testImplementation 'org.amshove.kluent:kluent:1.34'
	testImplementation "org.robolectric:robolectric:3.6.1"
}

apply plugin: 'com.google.gms.google-services'
